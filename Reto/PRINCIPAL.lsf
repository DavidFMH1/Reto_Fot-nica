###Todas las unidades estan dadas en micrometros

clear;

switchtolayout;                                       #inicio del diseño de la guia

###################################### PARAMETROS DE ENTRADA##################################################################

Alt_SiO2=1;                                           #Altura del SiO2
Anc_SiO2=7;                                           #Ancho del SiO2
Alt_Si=Alt_SiO2;                                      #Altura del Si                      
Anc_Si=Anc_SiO2;                                      #Ancho del Si         

resol_x_FDE=50;                                       #Resolucion en x de la malla FDE
resol_y_FDE=50;                                       #Resolucion en y de la malla FDE
sale_FDE=1;                                           #Medida entre el borde del nucleo y la malla FDE 
 
Alt_ini=0.4; Alt_fin=0.6; num_puntos_Alt=10;                        #Altura inicial y final del Si3N4 y numero de alturas
Anc_ini=0.5; Anc_fin=1; num_puntos_Anc=10;                        #Ancho inicial y final del Si3N4  y numero de anchos
lamb_ini=0.72; lamb_fin=0.89; num_puntos_lamb=32;                     #Longitud de onda inicial y final y numero de longitudes de onda

Alt_Si3N4=linspace(Alt_ini,Alt_fin,num_puntos_Alt);                  #Vector  de alturas del Si3N4
Anc_Si3N4=linspace(Anc_ini,Anc_fin,num_puntos_Anc);                  #Vector de anchos del Si3N4
lamb=linspace(lamb_ini,lamb_fin,num_puntos_lamb);                    #Vector de longitudes de onda

#################### PARAMETROS PARA BUSCAR EN UN RANGO DE INDICES O CERCA DE UN INDICE ######################################  

near_n=0;                                                #poner "0" para buscar en el rango min_n, max_n y "1" para buscar alrededor de n_near
n_near = 1.52;                                           #indice alrededor del cual se calculan los modos (solo sirve cuando se activa el "near")
min_n= 1.4;                                              #indice inferior del rango en el que se buscan los modos (solo sirve cuando se activa "in range")
max_n =2.2;                                              #indice superior del rango en el que se buscan los modos (solo sirve cuando se activa "in range")

###################################### HASTA AQUÍ PARAMETROS DE ENTRADA ######################################################



##############################################################################################################################

if (near_n == 1){                                       #Con estas lineas se activa la busqueda alrededor del indice n_near siempre que near_n=1
    setanalysis("search", "near n");    
    setanalysis("n",n_near);
    setanalysis("use max index",0);
    }
    
if (near_n == 0){                                       #Con estas lineas se activa la busqueda en un rango de indices siempre que near_n=0
    setanalysis("search", "in range");    
    setanalysis("n1",max_n);
    setanalysis("n2",min_n);
    }    

Data = zeros(num_puntos_Alt, num_puntos_Anc, num_puntos_lamb,5);
dataset = "/A";
fname = "Datos.h5";

rm(fname);

#################### CREACION DE MATRICES Y ARCHIVOS PARA ALMACENAR LA INFORMACION ###########################################

for(kk=1:num_puntos_Alt) {                             #Se abre el for para variaciones de las alturas
for(ii=1:num_puntos_Anc) {                            #Se abre el for para variaciones de los anchos
for(nn=1:num_puntos_lamb) {                           #Se abre el for para variaciones de longitudes de onda

setanalysis("wavelength", lamb(nn)*1e-6);           #Configura la longitud de onda de trabajo en la ventana set analisis    
    
Geometria;
Mallas;

nmodes = findmodes;                                   #Calcula los modos de acuerdo a la estructura configurada

selectmode('mode1');                                             #Selecciona el modo fundamental TE par
neff_modo1 = real(getdata('mode1',"neff"));                      #Extrae el indice efectivo del modo fundamental TE par
A_effmodo1 = real(getdata('mode1',"mode effective area"));

Data(kk,ii,nn,1) = Alt_Si3N4(kk);
Data(kk,ii,nn,2) = Anc_Si3N4(ii);
Data(kk,ii,nn,3) = lamb(nn);
Data(kk,ii,nn,4) = neff_modo1;
Data(kk,ii,nn,5) = A_effmodo1;

}       #Se cierra el for para variaciones de las alturas
}       #Se cierra el for para variaciones de los anchos
}       #Se cierra el for para variaciones de las longitudes de onda

h5write(fname, dataset, Data);
